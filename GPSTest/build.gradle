apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30
    buildToolsVersion '29.0.3'

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 29
        multiDexEnabled true
        // versionCode scheme - first two digits are minSdkVersion, last three digits are build number
        versionCode 18082
        versionName "3.9.5"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "map"

    productFlavors {
        google {
            // Google Play release using Google Play Services for maps
            dimension "map"
            applicationId "com.android.gpstest"
        }

        osmdroid {
            // F-Droid release using osmdroid for maps
            dimension "map"
            applicationId "com.android.gpstest.osmdroid"
        }
    }

    // http://stackoverflow.com/questions/20673625/gradle-0-7-0-duplicate-files-during-packaging-of-apk
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        disable 'MissingTranslation', 'ExtraTranslation'
    }

    if (project.hasProperty("secure.properties")
            && new File(project.property("secure.properties")).exists()) {

        Properties props = new Properties()
        props.load(new FileInputStream(file(project.property("secure.properties"))))

        signingConfigs {
            debug {
                storeFile file("gpstest.debug.keystore")
            }

            release {
                storeFile file(props['key.store'])
                keyAlias props['key.alias']
                storePassword props['key.storepassword']
                keyPassword props['key.keypassword']
            }
        }
    } else {
        signingConfigs {
            debug {
                storeFile file("gpstest.debug.keystore")
            }

            release {
                // Nothing here
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
        // Append the version name to the end of aligned APKs
        android.applicationVariants.all { variant ->
            if (variant.buildType.name == "release") {
                variant.outputs.all { output ->
                    outputFileName = "${variant.name}-v${variant.versionName}.apk"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources true
    }

    // Gradle automatically adds 'android.test.runner' as a dependency.
    useLibrary 'android.test.runner'

    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-rc1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:2.1.0-beta01"

    // Sliding drawer in map view
    implementation 'com.sothree.slidinguppanel:library:3.4.0'

    // Graphs for sliding drawer
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0-alpha'

    // QR Code reader for ground truth locations
    implementation 'com.google.zxing:android-integration:3.3.0'

    // Uploading device properties on user request
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'
    implementation 'commons-io:commons-io:2.6'

    // Share dialog
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    // Write GNSS antenna info to JSON
    implementation 'com.fasterxml.jackson.core:jackson-core:2.11.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.2'

    // Multidex - Needed for APIs < 21
    implementation 'androidx.multidex:multidex:2.0.1'

    // Map (Google flavor only)
    googleImplementation 'com.google.android.gms:play-services-maps:16.1.0'
    // Calculating offset for camera target in map view (Google flavor only)
    googleImplementation 'com.google.maps.android:android-maps-utils:0.5'
    // OSM Droid (fdroid flavor only)
    osmdroidImplementation 'org.osmdroid:osmdroid-android:6.1.4'

    //
    // Unit tests
    //
    testImplementation 'junit:junit:4.13.1'
    // Core library
    androidTestImplementation 'androidx.test:core:1.3.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'

    // Assertions
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
}